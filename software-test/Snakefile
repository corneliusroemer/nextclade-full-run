subsample_ratio = 0.0001
split_number = 4
url = {
    "old": "https://github.com/nextstrain/nextclade/releases/download/1.5.1/nextclade-Linux-x86_64",
    "new": "https://1104-271799467-gh.circle-artifacts.com/0/nextclade-Linux-x86_64"
}

wildcard_constraints:
    version="[^_]*",
    part="[^_]*",
    types="[^_]*",

rule all:
    input: expand("nextclade_{types}.diff",types=["clades","qc","score","alignment","errors"])

rule download_sequences:
    output: "data/sequences.fasta.xz"
    shell: "aws s3 cp s3://nextstrain-ncov-private/sequences.fasta.xz {output}"

rule subsample_sequences:
    input: rules.download_sequences.output
    output: "data/subsample.fasta.gz"
    shell: "xz -dc -T0 {input} | seqkit sample -p {subsample_ratio} -o {output}"

rule split_sequences:
    input: rules.subsample_sequences.output
    output: temp(expand("split/{{filename}}.part_{part:03d}.fasta.gz", part=range(1,split_number+1)))
    params: lambda w: f"data/{w.filename}.fasta.gz"
    shell:
        """
        cp {input} {params}; \
        seqkit split2 {params} -p {split_number} -O split; \
        rm {params}
        """

rule unzip_split:
    input: "split/{prefix}_subsample.part_{part}.fasta.gz"
    output: temp("split/{prefix}_subsample.part_{part}.fasta")
    shell: "gunzip -c {input} > {output}"

rule download_nextclade_binary:
    output: "bin/nextclade_{version}"
    shell: "curl -L {url[new]} -o {output}; chmod +x {output}"

rule download_nextclade_dataset:        
    input:
        binary = "bin/nextclade_new"
    output: directory("data/nextclade_dataset")
    params:
        binary = "./bin/nextclade_new",
    shell: "{params.binary} dataset get --name='sars-cov-2' --output-dir={output}"

rule run_nextclade:
    input:
        sequences = "split/all_subsample.part_{part}.fasta",
        dataset = rules.download_nextclade_dataset.output,
        binary = rules.download_nextclade_binary.output
    output:
        output_tsv = temp("results/nextclade_results_{version}_{part}.tsv"),
    params:
        output_alignments = "data/alignments_{version}_{part}",
        binary = "./bin/nextclade_{version}",
    threads: 4
    shell:
        """
         {params.binary} run \
            -j{threads} \
            --in-order \
            -i {input.sequences} \
            --input-dataset {input.dataset} \
            -t {output.output_tsv} \
            -d {params.output_alignments};
        rm -r {params.output_alignments}; \
        """

rule collect_nextclade_results:
    input: expand("results/nextclade_results_{{version}}_{part:03d}.tsv", part=range(1,split_number+1))
    output: "nextclade_{version}.tsv"
    shell: "keep-header {input} -- cat | dos2unix > {output}"

rule split_nextclade_results:
    input: "nextclade_{version}.tsv"
    output:
        clades = "nextclade_{version}_clades.tsv",
        qc = "nextclade_{version}_qc.tsv",
        score = "nextclade_{version}_score.tsv",
        alignment = "nextclade_{version}_alignment.tsv",
        errors = "nextclade_{version}_errors.tsv",
    shell:
        """
        tsv-select -H -f seqName,clade {input} > {output.clades};
        tsv-select -H -f seqName,qc.overallStatus {input} > {output.score};
        tsv-select -H -f seqName,qc.overallScore,qc.overallStatus,qc.missingData.score,qc.missingData.status,\
qc.missingData.totalMissing,qc.mixedSites.score,qc.mixedSites.status,qc.mixedSites.totalMixedSites,\
qc.privateMutations.excess,qc.privateMutations.score,qc.privateMutations.status,qc.privateMutations.total,\
qc.snpClusters.clusteredSNPs,qc.snpClusters.score,qc.snpClusters.status,qc.snpClusters.totalSNPs,\
qc.frameShifts.frameShifts,qc.frameShifts.totalFrameShifts,qc.frameShifts.frameShiftsIgnored,\
qc.frameShifts.totalFrameShiftsIgnored,qc.frameShifts.score,qc.frameShifts.status,qc.stopCodons.stopCodons,\
qc.stopCodons.totalStopCodons,qc.stopCodons.score,qc.stopCodons.status \
            {input} > {output.qc};
        tsv-select -H -f seqName,totalSubstitutions,totalDeletions,totalInsertions,totalFrameShifts,totalAminoacidSubstitutions,\
totalAminoacidDeletions,totalMissing,totalNonACGTNs,totalPcrPrimerChanges,substitutions,deletions,insertions,\
frameShifts,aaSubstitutions,aaDeletions,missing,nonACGTNs,pcrPrimerChanges,alignmentScore,alignmentStart,alignmentEnd \
            {input} > {output.alignment};
        tsv-select -H -f seqName,errors {input} > {output.errors};
        """

rule diff:
    input:
        old = "nextclade_old_{analysis}.tsv",
        new = "nextclade_new_{analysis}.tsv"
    output: "nextclade_{analysis}.diff"
    shell: "diff {input.old} {input.new} >{output}  || true"
